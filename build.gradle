buildscript {
    dependencies {
        classpath 'com.github.dexman545:Outlet:1.3.10'
    }
}
plugins {
    id 'java-library'
    id 'fabric-loom' version '0.12-SNAPSHOT'
    id 'maven-publish'
    id 'com.modrinth.minotaur' version '2.+'
    id 'org.ajoberstar.reckon' version '0.13.0'
}
apply plugin: 'dex.plugins.outlet'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    withSourcesJar()
}

outlet.allowSnapshotsForProject = false
outlet.mcVersionRange = project.minecraft_version_range

group = project.group
description = project.displayname
archivesBaseName = project.name

loom {
    mixin.defaultRefmapName = 'psychedelicraft.mixin.refmap.json'
    accessWidenerPath = file('src/main/resources/psychedelicraft.aw')
}

reckon {
    scopeFromProp()
    stageFromProp('beta', 'rc', 'final')
}

repositories {
    //mavenLocal()
    maven { name 'modmenu'; url 'https://maven.terraformersmc.com/releases' }
    maven { name 'minelp-releases'; url 'https://repo.minelittlepony-mod.com/maven/release' }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modApi "net.fabricmc:fabric-loader:${project.loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // provides networking and mod handshaking.
    //   https://github.com/Sollace/Fabwork
    // > gradlew build publishToMavenLocal
    modApi "com.sollace:fabwork:${project.fabwork_version}"
    include "com.sollace:fabwork:${project.fabwork_version}"

    modCompileOnly("com.terraformersmc:modmenu:${project.modmenu_version}")
}

processResources {
    inputs.property "version", project.version.toString()

    filesMatching("fabric.mod.json") {
        expand "version": project.version.toString()
    }

    from 'LICENSE'
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

tasks.modrinth {
    onlyIf {
      def stage = version.version.stage.value
      stage == null || stage.name != 'beta'
    }
}

modrinth {
    token = System.env.MODRINTH_KEY
    projectId = project.modrinth_project_id
    //featured = true
    versionNumber = version.toString()
    versionName = archivesBaseName + '-' + version
    changelog = "[Changelog](https://github.com/Sollace/Psychedelicraft/releases/tag/${version.toString()})"
    uploadFile = remapJar
    outlet.mcVersions().each{ver ->
      gameVersions.add ver
    }
    dependencies {
       required.project 'P7dR8mSH'
    }
}
